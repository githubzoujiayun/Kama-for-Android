apply plugin: 'android-library'
apply plugin: 'github-pages'

apply from: '../generateJavadoc.gradle'
apply from: '../pushMaven.gradle'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-core:2.3.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.4'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.3.4'

    compile 'net.smartam.leeloo:oauth2-client:0.1'
    compile 'net.smartam.leeloo:oauth2-common:0.1'
    compile 'net.smartam.leeloo:oauth2-httpclient4:0.1'

    compile 'com.intellij:annotations:12.0'

    /* Test libraries */

    androidTestCompile('org.mockito:mockito-core:1.9.5') { exclude group: 'org.hamcrest' }
//    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.1') { exclude group: 'org.hamcrest' }
    androidTestCompile('junit:junit:4.11') { exclude group: 'org.hamcrest' }
    androidTestCompile 'org.hamcrest:hamcrest-all:1.3'
    androidTestCompile 'commons-io:commons-io:+'

    /* Custom compiled dexmaker. Switch back to the original one once fix https://code.google.com/p/dexmaker/issues/detail?id=28 has been applied */
    androidTestCompile files('libs/dexmaker-1.1.jar')
    androidTestCompile files('libs/dexmaker-mockito-1.1-custom.jar')
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 1
        versionName VERSION_NAME
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }

    lintOptions {
        lintConfig file("../lint.xml")
    }
}

import org.ajoberstar.grgit.*
def repo = Grgit.open(".")

def branch = repo.branch.current.name
if(project.hasProperty('BRANCH')) {
    branch = BRANCH
}

githubPages {

    repoUri = 'https://github.com/label305/Kama-for-Android.git'
    commitMessage = 'Updated docs for branch ' + branch
    pages {
        from './build/docs/'
    }
    credentials {
        username = System.getenv('GH_TOKEN_KAMA')
        password = '\n'
    }
}